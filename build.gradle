import org.gradle.internal.jvm.Jvm

plugins {
  alias(libs.plugins.kotlin.multiplatform)
  alias(libs.plugins.mavenPublish)
  alias(libs.plugins.spotless)
  alias(libs.plugins.animalsniffer) apply false
}

kotlin {
  jvm {
    withJava() // Required for Animal Sniffer to work.
  }
}

spotless {
  kotlin {
    ktlint(libs.ktlint.get().version)
      .editorConfigOverride([
        // Making something an expression body should be a choice around readability.
        'ktlint_standard_function-expression-body': 'disabled',
      ])
    target 'src/**/*.kt'
  }
}

dependencies {
  testImplementation libs.kotlin.test.junit
}

// Animal Sniffer only works on JDK 11 or older currently.
if (!Jvm.current().javaVersion.isJava12Compatible()) {
  plugins.apply(libs.plugins.animalsniffer.get().pluginId)

  animalsniffer {
    sourceSets = [sourceSets.main] // Only check main sources, ignore test code.
  }
  dependencies {
    signature 'org.codehaus.mojo.signature:java18:1.0@signature'
    signature 'net.sf.androidscents.signature:android-api-level-21:5.0.1_r2@signature'
  }
}
